/**
 * Modified by Serhii Ovsiuk for compatibility with orientDb v.2.1.12
 * Added 'com.springsource.org.apache.commons.codec' dependency to jar for clearing dependency from project which use this project as dependency.
 */

buildscript {
    repositories {
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'osgi'
apply plugin: 'idea'
apply plugin: 'eclipse'

// For testing
//apply plugin: 'groovy'

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven {
        url "https://repos.inhabitech.com:8082/repository/smartspaces-io"
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group='io.smartspaces'
version = '0.3.0-SNAPSHOT'

ext {
    osgiVersion = "0.3.0.SNAPSHOT"
}

configurations {
    // configuration that holds jars to include in the jar
    extraLibs
}

dependencies {
    compile('org.apache.servicemix.bundles:org.apache.servicemix.bundles.quartz:2.3.1_1') {
        exclude group: 'c3p0', module: 'c3p0'
        transitive = false
    }
    extraLibs group: 'org.apache.commons', name: 'com.springsource.org.apache.commons.codec', version: '1.6.0'

    configurations.compile.extendsFrom(configurations.extraLibs)

    compile("org.slf4j:slf4j-api:1.7.10")
    compile('com.orientechnologies:orientdb-core:3.0.22')
    compile('com.orientechnologies:orientdb-client:3.0.22')
    compile('com.orientechnologies:orientdb-server:3.0.22') {
        exclude group: 'javax.mail', module: 'mail'
        transitive = false
    }
    //compile('com.orientechnologies:orientdb-enterprise:2.2.7')

    runtime("org.slf4j:slf4j-jdk14:1.7.26")

    //testCompile("joda-time:joda-time:2.8.2")
    //testCompile("org.slf4j:slf4j-simple:1.7.10")
    //testCompile("org.codehaus.groovy:groovy-all:2.4.1")
    //testCompile("org.spockframework:spock-core:1.0-groovy-2.4")
    //testRuntime("cglib:cglib-nodep:3.1")
    //testRuntime("org.objenesis:objenesis:2.1")
}

artifacts {
    archives jar
}

jar {
    baseName = 'quartz-orientdb'

    manifest {
        version = osgiVersion
        symbolicName = 'quartz.orientdb'

        instruction 'Export-Package',
                '!io.smartspaces.scheduling.quartz.orientdb.internal.*',
                'io.smartspaces.scheduling.quartz.orientdb.*'

        instruction 'Private-Package',
                'io.smartspaces.scheduling.quartz.orientdb.internal.*'

        instruction 'Import-Package',
                '*'
    }
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: mavenLocal().url)
            pom.groupId = rootProject.group
            pom.artifactId = 'quartz-orientdb'
            pom.version = rootProject.version
            pom.dependencies.removeAll {it.artifactId == 'com.springsource.org.apache.commons.codec'}
        }
//        mavenDeployer {
//            repository(url: "${releaseUrl}") {
//                authentication(userName: "${username}", password: "${password}")
//            }
//            snapshotRepository(url: "${snapshotUrl}") {
//                authentication(userName: "${username}", password: "${password}")
//            }
//            pom.artifactId = 'quartz-orientdb'
//            pom.dependencies.removeAll {it.artifactId == 'com.springsource.org.apache.commons.codec'}
//        }
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '4.5.1'
}
